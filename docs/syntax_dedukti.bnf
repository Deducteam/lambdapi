<qid> ::= [<uid>'.']+<uid>

<assert> ::= '#ASSERT'
           | '#ASSERTNOT'

<command> ::= <modifier>* <uid> <param>* ':' <term> '.'
            | <modifier>* 'def' <uid> ':' <term> '.'
            | <modifier>* 'def' <uid> ':' <term> ':=' <term> '.'
            | <modifier>* 'def' <uid> ':=' <term> '.'
            | <modifier>* 'def' <uid> <param>+ ':' <term> ':=' <term> '.'
            | <modifier>* 'def' <uid> <param>+ ':=' <term> '.'
            | <modifier>* 'thm' <uid> ':' <term> ':=' <term> '.'
            | <modifier>* 'thm' <uid> <param>+ ':' <term> ':=' <term> '.'
            | <rule>+ '.'
            | '#EVAL' <term> '.'
            | '#EVAL' <eval_config> <term> '.'
            | '#INFER' <term> '.'
            | '#INFER' <eval_config> <term> '.'
            | <assert> <aterm> ':' <term> '.'
            | <assert> <aterm> '==' <term> '.'
            | '#REQUIRE' <qid> '.'
            

<eval_config> ::= '[' <uid> ']'
                | '[' <uid> ',' <uid> ']'

<param> ::= '(' <uid> ':' <term> ')'

<modifier> ::= 'private'
             | 'injective'

<context_item> ::= <uid> [':' <term>]

<rule> ::= '[' [<context_item> (',' <context_item>)*] ']' <term>
           '-->' <term>

<sterm> ::= <qid>
          | <uid>
          | '_'
          | 'Type'
          | '(' <term> ')'

<aterm> ::= <sterm>+

<type_annot> ::= ':' <aterm>

<term> ::= <aterm>
         | <uid> ':' <aterm> '->' <term>
         | '(' <uid> ':' <aterm> ')' '->' <term>
         | <term> '->' <term>
         | '_' [<type_annot>] '=>' <term>
         | <uid> [<type_annot>] '=>' <term>


