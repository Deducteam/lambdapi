<command> ::= <modifier>* ID <param>* COLON <term> DOT
            | <modifier>* KW_DEF ID COLON <term> DOT
            | <modifier>* KW_DEF ID COLON <term> DEFEQ <term> DOT
            | <modifier>* KW_DEF ID DEFEQ <term> DOT
            | <modifier>* KW_DEF ID <param>+ COLON <term> DEFEQ <term> DOT
            | <modifier>* KW_DEF ID <param>+ DEFEQ <term> DOT
            | <modifier>* KW_THM ID COLON <term> DEFEQ <term> DOT
            | <modifier>* KW_THM ID <param>+ COLON <term> DEFEQ <term> DOT
            | <rule>+ DOT
            | EVAL <term> DOT
            | EVAL <eval_config> <term> DOT
            | INFER <term> DOT
            | INFER <eval_config> <term> DOT
            | ASSERT <aterm> COLON <term> DOT
            | ASSERT <aterm> EQUAL <term> DOT
            | REQUIRE DOT
            | EOF

<eval_config> ::= L_SQB ID R_SQB
                | L_SQB ID COMMA ID R_SQB

<param> ::= L_PAR ID COLON <term> R_PAR

<modifier> ::= KW_PRV
             | KW_INJ

<context_item> ::= ID [COLON <term>]

<rule> ::= L_SQB [<context_item> (COMMA <context_item>)*] R_SQB <term> LARROW
           <term>

<sterm> ::= QID
          | ID
          | WILD
          | TYPE
          | L_PAR <term> R_PAR

<aterm> ::= <sterm>+

<type_annot> ::= COLON <aterm>

<term> ::= <aterm>
         | ID COLON <aterm> ARROW <term>
         | L_PAR ID COLON <aterm> R_PAR ARROW <term>
         | <term> ARROW <term>
         | WILD [<type_annot>] FARROW <term>
         | ID [<type_annot>] FARROW <term>


